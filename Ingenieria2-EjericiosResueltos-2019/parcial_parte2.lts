//ej5 parcial '17
/*
* se llega a un deadlock si el brazo esta ocupado
y la prensa esta ocupada, esto se puedo solucionar
agregando un LOCK.
*/

const N = 3
BUFF = (in -> out -> BUFF).

BRAZO = ( sacar_de_cinta -> poner_en_maquina -> BRAZO
        | sacar_de_maquina -> ( poner_en_cinta -> BRAZO
                              | descartar -> BRAZO)
        ).

// este lock pone la siguiente limitacion:
//  "Un brazo puede hacer siempre lo que quiera, menos sacar un elemento de la cinta
//       cuando su maquina este ocupada."      --    Lucas, circa 2020.

LOCK = ({sacar_de_maquina, sacar_de_cinta, descartar, poner_en_cinta} -> LOCK
        |poner_en_maquina -> OCUPADO),
OCUPADO = ({sacar_de_maquina, descartar} -> LOCK).

MAQ = (recibir -> accion -> devolver -> MAQ).

||CINTA = ([1..N]:BUFF)/{in/[1].in, out/[N].out, [i:1..N].out/[i+1].in}@{in, out}.
||FAB = (([1]:CINTA)/{[1].sacar_de_cinta/[1].out, in/[1].in}
       ||([2]:CINTA)/{[2].sacar_de_cinta/[2].out}
       ||([3]:CINTA)/{out/[3].out}
    
       ||MAQ/{prensar/accion, poner_en_prensa/recibir, sacar_de_prensa/devolver}
       ||MAQ/{perforar/accion, poner_en_perforadora/recibir, sacar_de_perforadora/devolver}
       ||([1]:BRAZO)/{poner_en_prensa/[1].poner_en_maquina, 
                      sacar_de_prensa/[1].sacar_de_maquina,
                      [2].in/[1].poner_en_cinta}
       ||([2]:BRAZO)/{poner_en_perforadora/[2].poner_en_maquina, 
                      sacar_de_perforadora/[2].sacar_de_maquina,
                      [3].in/[2].poner_en_cinta}
       ||LOCK/{sacar_de_prensa/sacar_de_maquina,  // comentar para permitir deadlock
               poner_en_prensa/poner_en_maquina,
               [1].sacar_de_cinta/sacar_de_cinta,
               [1].descartar/descartar,
               [2].in/poner_en_cinta}
       ||LOCK/{sacar_de_perforadora/sacar_de_maquina,  // comentar para permitir deadlock
               poner_en_perforadora/poner_en_maquina,
               [2].sacar_de_cinta/sacar_de_cinta,
               [2].descartar/descartar,
               [3].in/poner_en_cinta}
        ).

progress PRENSANDO = {prensar}
progress PERFORANDO = {perforar}
progress SALIENDO = {out}
