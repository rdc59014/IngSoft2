const Arm1 = 4
const Arm2 = 5

BUF = (put -> get -> BUF).

// Cinta transportadora
||CONV(N=2) = ([0..N]:BUF) / {
	[i:1..N].put / [i - 1].get,
	put / [0].put,
	get / [N].get
	} @ {get, put}.

// Brazo
ARM = (get -> give -> (receive -> discard -> ARM
			   		  |receive -> put -> ARM)).

// Prensa
PRESS = (take -> press -> release -> PRESS).

// Taladro
DRILL = (take -> drill -> release -> DRILL).

// 3 cintas, 2 brazos, 1 prensa, 1 taladro
||MOD = ([1..3]:CONV || [Arm1..Arm2]:ARM || press:PRESS || drill:DRILL) / {
	put / [1].put,
	get / [3].get,
	[i:Arm1..Arm2].get / [i-3].get,
    [i:Arm1..Arm2].put / [i-2].put,
	[Arm1].give / press.take,
	[Arm1].receive / press.release,
	[Arm2].give / drill.take,
	[Arm2].receive / drill.release
	}.

// Para checkear progreso: Check -> Progress
progress P1 = {drill.drill}